buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "http://repo.spongepowered.org/maven"}
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6+'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

def modConfig
def apiConfig

file "./config.json" withReader {
    modConfig = new groovy.json.JsonSlurper().parseText(it.text)
}

file "./api/config.json" withReader {
    apiConfig = new groovy.json.JsonSlurper().parseText(it.text)
}

sourceSets.api {
    java {
        srcDirs 'api/'
    }
}
sourceSets.main {
    java {
        srcDirs 'src/main/java'
    }
}
sourceSets.test {
    java {
        srcDirs 'tests/'
    }
}

def combinedMinorVersion = (modConfig.version.minor | (apiConfig.version.minor<<8))

def shortVersion = "${modConfig.version.major}.${apiConfig.version.major}.${combinedMinorVersion}.${modConfig.version.patch}"
version = "${modConfig.version.minecraft}-${shortVersion}"

println "Current version is ${version}"
group= "dev.satyrn.wolfarmor"
archivesBaseName = "wolfarmor"

task updateModVersion(type:Copy) {
    from('src/main/java') {
        exclude 'mcmod.info'
    }
    into 'build/sources/main/java'
    expand 'version':shortVersion, 'mcversion':project.minecraft.version
}

task updatePatchNumber() {
    dependsOn build
    gradle.taskGraph.whenReady {taskGraph ->
        if(taskGraph.hasTask(compileJava) &&
           taskGraph.hasTask(assemble) &&
           taskGraph.hasTask(build)) {
            modConfig.version.patch += 1
            def configFile = new File('./config.json')
            configFile.delete()
            configFile.write(new groovy.json.JsonBuilder(modConfig).toPrettyString())
            println "Patch number updated to ${modConfig.version.patch}"
        }
    }
}

compileJava.dependsOn updateModVersion
updatePatchNumber.shouldRunAfter build

//noinspection GroovyUnusedAssignment
sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

build {
}

minecraft {
    version = "${modConfig.version.minecraft}-${modConfig.forge.version}"
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = modConfig.mcp.version
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    maven { url = 'http://repo.spongepowered.org/maven' }
}

dependencies {
    compile 'org.spongepowered:mixin:0.7.5-SNAPSHOT'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':shortVersion, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

	// copy LICENSE
    from('/') {
        include 'LICENSE'
        include 'CREDITS'
    }
}
