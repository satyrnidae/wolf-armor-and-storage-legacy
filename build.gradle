//import net.minecraftforge.gradle.common.task.SignJar
import groovy.json.JsonBuilder
import groovy.json.JsonSlurper

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5+'
        classpath 'org.spongepowered:mixingradle:0.7+'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'groovy'
    id 'scala'
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

repositories {
    maven { name = "SpongeMaven"; url = 'https://repo.spongepowered.org/maven' }
    maven { name = "sonatype"; url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { name = "satyrn.dev"; url = 'https://satyrn.dev/maven/' }
    maven { name = "CurseMaven"; url = 'https://cursemaven.com/' }
}

def config = null
file "${projectDir.getAbsolutePath()}/config.json" withReader {
    config = new JsonSlurper().parseText(it.text)
}

def shortVersion = "${config.version.major}.${config.version.minor}.${config.version.patch}"

version = "${config.minecraft}-${shortVersion}+forge"
group = 'dev.satyrn.wolfarmor'
archivesBaseName = 'wolf-armor-and-storage-legacy'

println "Current version is ${version}"

if (project.hasProperty('channel')) {
    version = version + "-${project.getProperties()['channel']}"
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: "${config.minecraft}-${config.forge}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    //implementation group: 'dev.satyrn.wolfarmor.api', name: 'wolfarmor-api', version: config.api
    implementation(group: 'org.spongepowered', name: 'mixin', version: '0.7+'){
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    compileOnly "curse.maven:letsleepingdogslie-334098:2803490"
    compileOnly "curse.maven:sophisticatedwolves-220495:2713186"

    implementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.7'
    implementation group: 'org.scala-lang', name: 'scala-library', version: '2.11.12'

    testImplementation group: 'org.scalatest', name: 'scalatest_2.11', version: '3.0.0'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    //testRuntime "org.objenesis:objenesis:2.6" //group: 'org.objenesis', name: 'objenesis', version: '2.6'
    //testRuntime group: 'net.bytebuddy', name: 'byte-buddy', version: '1.9.3'
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    //mappings channel: 'snapshot', version: '20171003-1.12'
    mappings channel: 'snapshot', version: '20171003-1.12'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

task cleanGeneratedSource(type: Delete) {
    delete "$buildDir/generated"
}

task generateSource(type: Copy) {
    dependsOn cleanGeneratedSource
    from sourceSets.main.java
    into "$buildDir/generated/sources/main/java"
    filesMatching('**/*.java') {
        expand version: shortVersion, mcversion: config.minecraft,
                modname: config.name, modid: config.modid,
                apiVersion: config.api
    }
}

compileJava {
    dependsOn generateSource
    source = project.fileTree("$buildDir/generated/sources/main/java")
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            'Built-By': System.properties['user.name'],
            'Created-By': "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})",
            'Implementation-Title': project.name,
            'Implementation-Version': shortVersion,
            'Implementation-Vendor': 'https://satyrn.dev',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'API-Version': config.api,
            'FMLCorePlugin': "${project.group}.core.WolfArmorCore",
            'ForceLoadAsMod': true,
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'MixinConfigs': 'mixins.wolfarmor.core.json',
            'FMLAT': 'accesstransformer.cfg'
        ])
    }
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', '1.12.2'

    duplicatesStrategy = DuplicatesStrategy.WARN

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand version: shortVersion, mcversion: '1.12.2', modname: config.name, modid: config.modid
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    // copy LICENSE
    from('/') {
        include 'LICENSE'
        include 'CREDITS'
        include 'THIRDPARTY'
    }
}

mixin {
    add sourceSets.main, "mixins.wolfarmor.refmap.json"
}


// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')


task saveConfig {
    doLast {
        def configFile = new File("${projectDir.getAbsolutePath()}/config.json")
        configFile.delete()
        configFile.write(new JsonBuilder(config).toPrettyString())
    }
}

def VERSIONING_GROUP = 'Versioning'
def SCM_GROUP = 'Source Control'

task versionPatch {
    group VERSIONING_GROUP
    dependsOn 'gitCheckWorkingTree'
    finalizedBy 'saveConfig'
    finalizedBy 'gitAddConfig'
    finalizedBy 'gitTagVersion'
    finalizedBy 'gitCommit'
    finalizedBy 'reportVersionChange'

    doLast {
        println projectDir.getAbsolutePath()
        if (gitCheckWorkingTree.output() != "") {
            saveConfig.enabled = false
            gitAddConfig.enabled = false
            gitTagVersion.enabled = false
            gitCommit.enabled = false
            throw new Error("Working directory not clean!")
        } else {
            config.version.patch += 1
        }
    }
}

task versionMinor {
    group VERSIONING_GROUP
    dependsOn 'gitCheckWorkingTree'
    finalizedBy 'saveConfig'
    finalizedBy 'gitAddConfig'
    finalizedBy 'gitTagVersion'
    finalizedBy 'gitCommit'
    finalizedBy 'reportVersionChange'

    doLast {
        println projectDir.getAbsolutePath()
        if (gitCheckWorkingTree.output() != "") {
            saveConfig.enabled = false
            gitAddConfig.enabled = false
            gitTagVersion.enabled = false
            gitCommit.enabled = false
            throw new Error("Working directory not clean!")
        } else {
            config.version.patch = 0
            config.version.minor += 1
        }
    }
}

task versionMajor {
    group VERSIONING_GROUP
    dependsOn 'gitCheckWorkingTree'
    finalizedBy 'saveConfig'
    finalizedBy 'gitAddConfig'
    finalizedBy 'gitTagVersion'
    finalizedBy 'gitCommit'
    finalizedBy 'reportVersionChange'

    doLast {
        println projectDir.getAbsolutePath()
        if (gitCheckWorkingTree.output() != "") {
            saveConfig.enabled = false
            gitAddConfig.enabled = false
            gitTagVersion.enabled = false
            gitCommit.enabled = false
            throw new Error("Working directory not clean!")
        } else {
            config.version.patch = 0
            config.version.minor = 0
            config.version.major += 1
        }
    }
}

task gitCheckWorkingTree(type:Exec) {
    group SCM_GROUP
    workingDir projectDir.getAbsolutePath()
    executable 'git'
    args '--no-pager', 'diff', 'HEAD'
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

task gitAddConfig() {
    group SCM_GROUP
    shouldRunAfter saveConfig
    doLast {
        exec {
            workingDir projectDir.getAbsolutePath()
            ignoreExitValue true
            executable 'git'
            args 'add', 'config.json'
        }
    }
}

task gitCommit() {
    group SCM_GROUP
    shouldRunAfter gitAddConfig
    doLast {
        exec {
            workingDir projectDir.getAbsolutePath()
            def newVersionNum = "v${config.version.major}.${config.version.minor}.${config.version.patch}"
            ignoreExitValue true
            executable 'git'
            if (project.hasProperty('signing.password')) {
                args 'commit', '-S', '-m', "\"üè∑ ${newVersionNum}\""
            } else {
                args 'commit', '-m', "\"üè∑ ${newVersionNum}\""
            }
        }
    }
}

task gitTagVersion() {
    group SCM_GROUP
    shouldRunAfter gitCommit
    doLast {
        exec {
            workingDir projectDir.getAbsolutePath()
            def newVersionNum = "v${config.version.major}.${config.version.minor}.${config.version.patch}"
            ignoreExitValue true
            executable 'git'
            if (project.hasProperty('signing.password')) {
                args 'tag', '-s', "${newVersionNum}", '-m', "\"${newVersionNum}\""
            } else {
                args 'tag', '-a', "${newVersionNum}", '-m', "\"${newVersionNum}\""
            }
        }
        exec {
            workingDir projectDir.getAbsolutePath()
            ignoreExitValue true
            executable 'git'
            args 'push', 'origin', '--tags'
        }
    }
}

task reportVersionChange {
    group VERSIONING_GROUP
    shouldRunAfter gitCommit
    doLast {
        println "Current semver: ${config.version.major}.${config.version.minor}.${config.version.patch}"
    }
}
