import net.minecraftforge.gradle.common.task.SignJar
import groovy.json.JsonBuilder
import groovy.json.JsonSlurper

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'http://repo.spongepowered.org/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7+'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'groovy'
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    maven { url = 'http://repo.spongepowered.org/maven' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://satyrn.dev/maven/' }
    maven { url = 'https://www.cursemaven.com/' }
}

configurations {
    shadow
}

def config = null
file "${projectDir.getAbsolutePath()}/config.json" withReader {
    config = new JsonSlurper().parseText(it.text)
}

def shortVersion = "${config.version.major}.${config.version.minor}.${config.version.patch}"

version = "${config.minecraft}-${shortVersion}"
group = 'dev.satyrn.wolfarmor'
archivesBaseName = 'wolfarmor'

println "Current version is ${version}"

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
        }
    }
    repositories {
        maven {
            credentials {
                username project.getProperties()['maven.username']
                password project.getProperties()['maven.password']
            }
            url 'sftp://ftp.satyrn.dev:22/maven'
        }
    }
}

if (project.hasProperty('channel')) {
    version = version + "-${project.getProperties()['channel']}"
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

dependencies {
    minecraft "net.minecraftforge:forge:${config.minecraft}-${config.forge}"

    compile "dev.satyrn.wolfarmor.api:wolfarmor-api:${config.api}"
    compile('org.spongepowered:mixin:0.8-SNAPSHOT'){
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    shadow "dev.satyrn.wolfarmor.api:wolfarmor-api:${config.api}"
    shadow('org.spongepowered:mixin:0.8-SNAPSHOT'){
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    compileOnly 'curse.maven:let-sleeping-dogs-lie:2803490'
    compileOnly 'curse.maven:sophisticated-wolves:2713186'
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    //mappings channel: 'snapshot', version: '20171003-1.12'
    mappings channel: 'snapshot', version: '20171003-1.12'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            property 'fml.coreMods.load', 'dev.satyrn.wolfarmor.core.WolfArmorCore'
            property 'mixin.debug', 'true'

            //TODO: Yggdrasil auth
        }

        server {

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
            property 'fml.coreMods.load', 'dev.satyrn.wolfarmor.core.WolfArmorCore'
            property 'mixin.debug', 'true'
        }
    }
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
    test {
        dependencies {
            testCompile 'org.codehaus.groovy:groovy-all:2.5.7'
            testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
            testCompile 'junit:junit:4.12'
            testRuntime 'org.objenesis:objenesis:2.6'
            testRuntime 'net.bytebuddy:byte-buddy:1.9.3'
        }
    }
}

compileJava {
    doFirst {
        copy {
            from sourceSets.main.allSource
            into "$buildDir/generated/main/java"
            filesMatching(['**/WolfArmorMod.java', '**/WolfArmorCore.java']) {
                expand version: shortVersion, mcversion: '1.12.2',
                        modname: config.name, modid: config.modid
            }
        }
    }
    source = project.fileTree("$buildDir/generated/main/java")
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            'Built-By': System.properties['user.name'],
            'Created-By': "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})",
            'Implementation-Title': project.name,
            'Implementation-Version': version,
            'Implementation-Vendor': 'https://satyrn.dev',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'API-Version': config.api,
            'FMLCorePlugin': "${project.group}.core.WolfArmorCore",
            'ForceLoadAsMod': true,
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'MixinConfigs': 'mixins.wolfarmor.core.json',
            'FMLAT': 'accesstransformer.cfg'
        ])
    }
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', '1.12.2'

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand version: shortVersion, mcversion: '1.12.2', modname: config.name, modid: config.modid
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    // copy LICENSE
    from('/') {
        include 'LICENSE'
        include 'CREDITS'
        include 'THIRDPARTY'
    }
}

mixin {
    add sourceSets.main, "mixins.wolfarmor.refmap.json"
}

task signJar(type: SignJar, dependsOn: shadowJar) {
    onlyIf {
        project.hasProperty('keyStore');
    }

    keyStore = project.getProperties()['keyStore'];
    alias = project.getProperties()['keyStoreAlias'];
    storePass = project.getProperties()['keyStorePass'];
    keyPass = project.getProperties()['keyStoreKeyPass'];
    inputFile = shadowJar.archivePath
    outputFile = "$buildDir/libs/$archivesBaseName-$version-signed.jar"
}

build {
    finalizedBy signJar
}

shadowJar {
    mainSpec.sourcePaths.clear()
    configurations = [ project.configurations.shadow ]
    dependsOn jar

    manifest {
        inheritFrom project.tasks.jar.manifest
        attributes(
                'FMLCorePluginContainsFMLMod': true
        )
    }

    exclude 'dummyThing'
    exclude 'LICENSES*'

    afterEvaluate {
        from jar
    }
}


// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')
publish.dependsOn('shadowJar')


task saveConfig {
    doLast {
        def configFile = new File("${projectDir.getAbsolutePath()}/config.json")
        configFile.delete()
        configFile.write(new JsonBuilder(config).toPrettyString())
    }
}

def VERSIONING_GROUP = 'Versioning'
def SCM_GROUP = 'Source Control'

task versionPatch {
    group VERSIONING_GROUP
    dependsOn 'gitCheckWorkingTree'
    finalizedBy 'saveConfig'
    finalizedBy 'gitAddConfig'
    finalizedBy 'gitTagVersion'
    finalizedBy 'gitCommit'
    finalizedBy 'reportVersionChange'

    doLast {
        println projectDir.getAbsolutePath()
        if (gitCheckWorkingTree.output() != "") {
            saveConfig.enabled = false
            gitAddConfig.enabled = false
            gitTagVersion.enabled = false
            gitCommit.enabled = false
            throw new Error("Working directory not clean!")
        } else {
            config.version.patch += 1
        }
    }
}

task versionMinor {
    group VERSIONING_GROUP
    dependsOn 'gitCheckWorkingTree'
    finalizedBy 'saveConfig'
    finalizedBy 'gitAddConfig'
    finalizedBy 'gitTagVersion'
    finalizedBy 'gitCommit'
    finalizedBy 'reportVersionChange'

    doLast {
        println projectDir.getAbsolutePath()
        if (gitCheckWorkingTree.output() != "") {
            saveConfig.enabled = false
            gitAddConfig.enabled = false
            gitTagVersion.enabled = false
            gitCommit.enabled = false
            throw new Error("Working directory not clean!")
        } else {
            config.version.patch = 0
            config.version.minor += 1
        }
    }
}

task versionMajor {
    group VERSIONING_GROUP
    dependsOn 'gitCheckWorkingTree'
    finalizedBy 'saveConfig'
    finalizedBy 'gitAddConfig'
    finalizedBy 'gitTagVersion'
    finalizedBy 'gitCommit'
    finalizedBy 'reportVersionChange'

    doLast {
        println projectDir.getAbsolutePath()
        if (gitCheckWorkingTree.output() != "") {
            saveConfig.enabled = false
            gitAddConfig.enabled = false
            gitTagVersion.enabled = false
            gitCommit.enabled = false
            throw new Error("Working directory not clean!")
        } else {
            config.version.patch = 0
            config.version.minor = 0
            config.version.major += 1
        }
    }
}

task gitCheckWorkingTree(type:Exec) {
    group SCM_GROUP
    workingDir projectDir.getAbsolutePath()
    executable 'git'
    args '--no-pager', 'diff', 'HEAD'
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

task gitAddConfig() {
    group SCM_GROUP
    shouldRunAfter saveConfig
    doLast {
        exec {
            workingDir projectDir.getAbsolutePath()
            ignoreExitValue true
            executable 'git'
            args 'add', 'config.json'
        }
    }
}

task gitCommit() {
    group SCM_GROUP
    shouldRunAfter gitAddConfig
    doLast {
        exec {
            workingDir projectDir.getAbsolutePath()
            def newVersionNum = "v${config.version.major}.${config.version.minor}.${config.version.patch}"
            ignoreExitValue true
            executable 'git'
            if (project.hasProperty('signing.password')) {
                args 'commit', '-S', '-m', "\"üè∑ ${newVersionNum}\""
            } else {
                args 'commit', '-m', "\"üè∑ ${newVersionNum}\""
            }
        }
    }
}

task gitTagVersion() {
    group SCM_GROUP
    shouldRunAfter gitCommit
    doLast {
        exec {
            workingDir projectDir.getAbsolutePath()
            def newVersionNum = "v${config.version.major}.${config.version.minor}.${config.version.patch}"
            ignoreExitValue true
            executable 'git'
            if (project.hasProperty('signing.password')) {
                args 'tag', '-s', "${newVersionNum}", '-m', "\"${newVersionNum}\""
            } else {
                args 'tag', '-a', "${newVersionNum}", '-m', "\"${newVersionNum}\""
            }
        }
        exec {
            workingDir projectDir.getAbsolutePath()
            ignoreExitValue true
            executable 'git'
            args 'push', 'origin', '--tags'
        }
    }
}

task reportVersionChange {
    group VERSIONING_GROUP
    shouldRunAfter gitCommit
    doLast {
        println "Current semver: ${config.version.major}.${config.version.minor}.${config.version.patch}"
    }
}
